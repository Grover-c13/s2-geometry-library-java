plugins {
    id 'java'
    id 'com.jfrog.bintray' version '1.1'
    id 'maven-publish'
}

group = 'org.jnegre.google'
version = '1.2-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDir 'src'
        }
    }
    test {
        java {
            srcDir 'tests'
        }
    }
}

dependencies {
    compile 'com.google.guava:guava:r09'
    compile 'com.google.code.findbugs:jsr305:3.0.0'

    testCompile 'junit:junit:4.11'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

bintray {
    //from gradle.properties
    user = project.hasProperty('bintrayUser')?project.property('bintrayUser'):'NO-USER'
    key = project.hasProperty('bintrayKey')?project.property('bintrayKey'):'NO-KEY'

    //dryRun = true

    publications = ['mavenJava']

    pkg {
        repo = 'maven'
        name = "${project.group}:${project.name}"

        issueTrackerUrl = 'https://github.com/jnegre/s2-geometry-library-java/issues'
        vcsUrl = 'https://github.com/jnegre/s2-geometry-library-java.git'
        licenses = ['Apache-2.0']
    }

}